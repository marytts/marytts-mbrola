plugins {
    id 'groovy'
    id 'de.undercouch.download' version '3.1.2'
    id 'maven-publish'
}

group 'de.dfki.mary'
version '1.0-SNAPSHOT'

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    integrationTestCompile {
        extendsFrom testCompile
    }
    integrationTestRuntime {
        extendsFrom testRuntime
    }
}

repositories {
    jcenter()
}

dependencies {
    compile group: 'de.dfki.mary', name: 'marytts-runtime', version: '5.2'
    testCompile group: 'org.testng', name: 'testng', version: '6.9.10'
}

task downloadMbrola(type: de.undercouch.gradle.tasks.download.Download) {
    src 'http://tcts.fpms.ac.be/synthesis/mbrola/bin/macintosh/mbrola'
    dest file("$processResources.destinationDir/marytts/mbrola/bin/mbrola-mac")
    overwrite false
    processResources.finalizedBy it
    doLast {
        ant.chmod(file: dest, perm: 'ugo+x')
    }
}

task integrationTest(type: Test, group: 'Verification', description: 'Runs the integration tests.') {
    check.dependsOn it
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperties << [
            'log4j.logger.marytts': 'DEBUG,stderr'
    ]
}

tasks.withType(Test) {
    useTestNG()
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url "$rootProject.buildDir/repo"
        }
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/marytts/marytts/marytts'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}
